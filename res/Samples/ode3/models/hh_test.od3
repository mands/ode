/* Test model for the Original HH model 
*/

module HodgkinHuxley {

    component c1(x, y) {
        return (x+y)
    }

    component membrane() {
        val Cm = 1
        val E_R = -75

        val i_Stim = {
            t >= 10 and t <= 10.5 : 20,
            default: 0
        }

        val i_K = potassium_channel(V, E_R)
        val i_Na = sodium_channel(V, E_R)
        val i_L = leakage_current(V, E_R)

        ode V = {initVal : -75, deltaVal: -(-i_Stim + i_Na + i_K + i_L) / Cm}

        return (V)
    }

    component leakage_current(V, E_R) {
        val g_L = 0.3
        val E_L = E_R + 10.613
        val i_L = g_L * (V - E_L)
        return (i_L)
    }

    component potassium_channel(V, E_R) {
        val n = potassium_channel_n_gate(V)
        val g_K = 36
        val E_K = E_R - 12
        val i_K = g_K * pow(n,4) * (V - E_K)
        return (i_K)
    }

    component potassium_channel_n_gate(V) {
        val alpha_n = (-0.01 * (V + 65)) / (exp(-(V + 65) / 10) - 1)
        val beta_n = 0.125 * exp((V+75) / 80)
        ode n = {initVal : 0.325, deltaVal: (alpha_n*(1-n) - (beta_n*n))}
        return (n)
    }

    component sodium_channel(V, E_R) {
        val m = sodium_channel_m_gate(V)
        val h = sodium_channel_h_gate(V)
        val g_Na = 120
        val E_Na = E_R + 115
        val i_Na = g_Na * pow(m, 3) * h * (V - E_Na)
        return (i_Na)
    }

    component sodium_channel_m_gate(V) { 
        val alpha_m = -0.1 * (V + 50) / (exp(-(V + 50) / 10) - 1)
        val beta_m = 4 * exp(-(V + 75) / 18)
        ode m = {initVal : 0.05, deltaVal: (alpha_m * (1 - m) - beta_m * m)}
        return (m)
    }

    component sodium_channel_h_gate(t, V) {
        val alpha_h = 0.07 * exp(-(V + 75) / 20)
        val beta_h = 1 / (exp(-(V + 45) / 10) + 1)
        ode h = {initVal : 0.6, deltaVal: (alpha_h * (1 - h) - beta_h * h)}
        return (h)
    }   
}
